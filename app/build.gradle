buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        classpath 'io.fabric.tools:gradle:1.+'
    }
}
apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

repositories {
    maven { url 'https://maven.fabric.io/public' }
}


android {
    signingConfigs {
        release {
            keyAlias ""
            keyPassword ""
            storeFile file('../extras/release.keystore')
            storePassword ""
        }
    }
    compileSdkVersion 23
    buildToolsVersion "23.0.2"

    defaultConfig {
        applicationId "com.kbeanie.multipicker.sample"
        minSdkVersion 16
        targetSdkVersion 23
        versionCode 6
        versionName "0.9.7"
    }
    buildTypes {
        release {
            minifyEnabled false
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    dexOptions {
        incremental true
    }
}

gradle.taskGraph.whenReady { taskGraph ->
    if (taskGraph.hasTask(':app:assembleRelease')) {
        /**
         * Configure Release key
         */
        def props = new Properties()
        props.load(new FileInputStream(rootProject.file("extras/secret.properties")))
        android.signingConfigs.release.keyAlias props.alias
        android.signingConfigs.release.storePassword props.getProperty('keyStorePassword')
        android.signingConfigs.release.keyPassword props.getProperty('keyAliasPassword')
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    testCompile 'junit:junit:4.12'
    compile 'com.android.support:appcompat-v7:23.1.1'
    compile 'com.google.android.gms:play-services-base:8.4.0'
    compile 'com.google.android.gms:play-services-ads:8.4.0'
    compile 'com.android.support:recyclerview-v7:23.1.1'
    compile 'com.github.bumptech.glide:glide:3.7.0'
//    compile 'com.kbeanie:multipicker:0.9.7@aar'
    compile project(':multipicker')
    compile('com.crashlytics.sdk.android:crashlytics:2.5.5@aar') {
        transitive = true;
    }
}
